#!/usr/bin/env gnuplot -d -c

# usage: gnuplot [infile] [date_start date_end]

FILE_SEPARATOR = ""
if (substr(GPVAL_PWD,1,1) eq '/') { FILE_SEPARATOR = '/' }
if (FILE_SEPARATOR eq "")         { FILE_SEPARATOR = '\' } # Assume windows

# Loads the default styles from the UTIL dir, relative to this file (ARG0).
#
# WARNING:  If you change the name of this file, you need to change the string
# in the strstrt function below
settings_relative_location = "..".FILE_SEPARATOR."util".FILE_SEPARATOR."gnuplot_default_settings"
load substr(ARG0, 1, strstrt(ARG0, FILE_SEPARATOR."top_info_plot.gnup")).settings_relative_location


# To switch to an SVG output, swap the comment for the `@USE_PNG_TERM` and the
# `@USE_SVG_TERM` lines

@USE_PNG_TERM
# @USE_SVG_TERM

set title system('echo ${TITLE:-Memory Allocations}')

input = ARG1
if (input eq "") input="tmp_plot.data"

# output filename is arg2. default tmp_plot.png

if (GPVAL_TERM eq 'png')    { FILE_EXT = "png"  }
if (GPVAL_TERM eq 'svg')    { FILE_EXT = "svg"  }

# FIXME:  Does not work with relative files with a `../`
output_file = substr(input,1,strstrt(input, ".")-1)
if (exists("ARG2")) output_file = output_file."_from_".strftime("%Y%m%d%H%M",strptime("%Y-%m-%dT%H:%M:%S",ARG2))
if (exists("ARG3")) output_file = output_file."_to_".strftime("%Y%m%d%H%M",strptime("%Y-%m-%dT%H:%M:%S",ARG3))

set output output_file.".".FILE_EXT


set timefmt "%Y-%m-%dT%H:%M:%S"
set format x "%m-%d\n%H:%M"
set xlabel "Time"
set xdata time
set xtic nomirror # show x tics on bottom
set mxtic 6 # minor tics every 5 minutes
if (exists("ARG2")) set xrange [ARG2 : ARG3]

unset x2tic

set ylabel "Memory"
set format y '%.1s %cB'
set autoscale y
set yrange [0: ] # axis start at 0
set ytics nomirror # show time ticks on left only (don't want to see next to right ticks)

unset y2tic

plot input using 1:2 t 'RSS'  with lines @THICK lc @TEAL   axes x1y1, \
     input using 1:3 t 'VIRT' with lines @THICK lc @GREEN  axes x1y1
